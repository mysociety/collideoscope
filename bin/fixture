#!/usr/bin/env perl
#
# This script will create a test body and its categories, covering the area
# provided, and users associated with that body, which should help testing
# of report interactions.

use strict;
use warnings;
use v5.14;

BEGIN {
    use File::Basename qw(dirname);
    use File::Spec;
    my $d = dirname(File::Spec->rel2abs($0));
    require "$d/../../fixmystreet/setenv.pl";
}

use Path::Tiny;
use FixMyStreet;
use FixMyStreet::Cobrand;
use FixMyStreet::DB::Factories;
use Getopt::Long::Descriptive;

my ($opt, $usage) = describe_options(
    '%c %o',
    [ 'empty', "Empty all tables of the database first" ],
    [ 'commit', "Actually commit changes to the database" ],
    [ 'help', "print usage message and exit", { shortcircuit => 1 } ],
);
print($usage->text), exit if $opt->help;

FixMyStreet::DB::Factories->setup($opt);

# Body + categories
my %severity = (miss => 10, slight => 30, serious => 60, fatal => 100);
my @participant = ('bicycle', 'generic', 'horse', 'pedestrian', 'vehicle');
my @road_types = ('road', 'lane-onroad', 'lane-separate', 'pavement');
my @categories = map { my $p = $_; map { "$p-$_" } keys %severity } @participant;

my $body = FixMyStreet::DB::Factory::Body->find_or_create({
    area_id => 2536,
    categories => \@categories,
    name => 'Sefton Borough Council',
});
say "Created body and categories";

# Users
say "Created users, all with password 'password':";
my %users;
foreach (
    { name => 'Wizard of Oz', email_verified => 1, email => 'admin@example.org', is_superuser => 't' },
    { name => "Norma User", email_verified => 1, email => 'user@example.org' },
) {
    $users{$_->{email}} = FixMyStreet::DB::Factory::User->find_or_create($_);
    my $perms = $_->{permissions} ? " (" . join(', ', @{$_->{permissions} || []}) . ")" : "";
    my $su = $_->{is_superuser} ? " (superuser)" : "";
    say "* $_->{email}$perms$su";
}

# Problems

my ($location, $latitude, $longitude) = ('PR8 1JE', 53.639957, -3.007618); # Sefton
my $cobrand = FixMyStreet::Cobrand::Smidsy->new;
my $cache_dir = path(FixMyStreet->config('UPLOAD_DIR'));
$cache_dir->mkpath;

my $user = $users{'user@example.org'};
my $num = 20;
say "Created $num problems around '$location' in cobrand '" . $cobrand->moniker . "'";
my $confirmed = DateTime->today->subtract(days => 1)->add(hours => 8);
my $incident = $confirmed->clone->subtract(weeks => 1, days => 2);
my $problems = [];
for (1..$num) {
    $confirmed->add(seconds => rand(7000));
    my $participant = $participant[int(rand(@participant))];
    my $severity = (keys %severity)[rand keys %severity];
    my $problem = FixMyStreet::DB::Factory::Problem->create({
        body => $body,
        external_body => int(rand(4)) ? undef : 'stats19',
        areas => ',2536,',
        user => $user,
        postcode => $location,
        latitude => $latitude + rand(2 * 0.01) - 0.01,
        longitude => $longitude + rand(3 * 0.01) - 1.5 * 0.01,
        category => "$participant-$severity",
        cobrand => $cobrand->moniker,
        confirmed => $confirmed,
        detail => 'Lots of detail here.',
        extra => {
            severity => $severity{$severity},
            participants => $participant,
            road_type => $road_types[int rand @road_types],
            incident_date => $incident->date,
            incident_time => $incident->time,
        },
    });
    $cobrand->report_new_munge_before_insert($problem);
    $problem->update;
    push @$problems, $problem;
}
